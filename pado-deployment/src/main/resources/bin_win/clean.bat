:: ========================================================================
:: Copyright (c) 2013-2015 Netcrest Technologies, LLC. All rights reserved.
::
:: Licensed under the Apache License, Version 2.0 (the "License");
:: you may not use this file except in compliance with the License.
:: You may obtain a copy of the License at
::
::     http://www.apache.org/licenses/LICENSE-2.0
::
:: Unless required by applicable law or agreed to in writing, software
:: distributed under the License is distributed on an "AS IS" BASIS,
:: WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
:: See the License for the specific language governing permissions and
:: limitations under the License.
:: ========================================================================

@echo off
@setlocal enabledelayedexpansion

@call setenv.bat
@call all_env.bat
@call argenv.bat %*

if "%1" == "-?" (
   echo Usage:
   echo Do NOT run this command while servers are running.
   echo It will remove the log and stats files.
   echo.
   echo    clean [-grid %GRIDS_OPT%] [-site %SITES_OPT%] [-all] [-locators] [-agents] [-persist] [-clients] [-?]
   echo.
   echo       Cleans up directories by removing log, stats, other temporary files
   echo       from the specified grid and site.
   echo.
   echo       -all       Removes all files generated in the specified grid and site
   echo       -grid      Removes log and stats files generated in the specified grid
   echo                  Default: %GRID_DEFAULT%
   echo       -site      Removes log and stats files generated in the specified site
   echo                  Default: %SITE_DEFAULT%
   echo       -locators  Removes locator files in addition to server files
   echo       -agents    Removes agent files in addition to server files
   echo       -persist   Removes persistent files generated by servers
   echo       -clients   Removes client files
   echo.
   echo    Default: clean -grid %GRID_DEFAULT% -site %SITE_DEFAULT%
   echo.
   goto stop
)

@call %GRIDS_DIR%\%GRID%\site_%SITE%.bat

@set SERVER_PREFIX=server-%GRID%-%SITE%

if "%RUN_DIR%" == "" (
   @set RUN_DIR=%BASE_DIR%\run
)
if "%LOG_DIR%" == "" (
   @set LOG_DIR=%BASE_DIR%/log
)
if "%STATS_DIR%" == "" (
   @set STATS_DIR=%BASE_DIR%/stats
)

:: locators and agents
if "%ALL%" == "true" (
   @set LOCATORS=true
   @set AGENTS=true
   @set PERSIST=true
)

if "%LOCATORS%" == "true" (
  erase /f /q %RUN_DIR%\%LOCATOR_ID%* > NUL 2>&1
)
if "%AGENTS%" == "true" (
   erase /f /q %RUN_DIR%\%AGENT_ID%* > NUL 2>&1
)
if "%CLIENTS%" == "true" (
   erase /f /q %LOG_DIR%\clien*.log > NUL 2>&1
   erase /f /q %STATS_DIR%\client*.gfs > NUL 2>&1
)

:: servers
for /l %%i in (1,1,%NUM_SERVERS%) do (
   if %%i LSS 10 (
      @set SERVER_NUM=0%%i
   ) else (
      @set SERVER_NUM=%%i
   )
   @set SERVER_ID=%SERVER_PREFIX%!SERVER_NUM!
   erase /f /q %LOG_DIR%\!SERVER_ID!.log %STATS_DIR%\!SERVER_ID!.gfs > NUL 2>&1
   if "%PERSIST%" == "true" (
      erase /f /q %RUN_DIR%\!SERVER_ID! > NUL 2>&1
   )
)

goto stop

:stop
